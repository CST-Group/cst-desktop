plugins {
    id 'java-library-distribution'
    id 'jacoco'
    id 'maven-publish'
}

group = 'com.github.CST-Group'

description = "CST-Desktop"

java {
   //sourceCompatibility = JavaVersion.VERSION_11
   //targetCompatibility = JavaVersion.VERSION_11
   //sourceCompatibility = JavaVersion.VERSION_1_8
   //targetCompatibility = JavaVersion.VERSION_1_8

    sourceCompatibility = JavaVersion.VERSION_17 // This is necessary due to cst-bindings with ROS2
    targetCompatibility = JavaVersion.VERSION_17

    toolchain {
        languageVersion = JavaLanguageVersion.of(17)  // This is necessary for jitpack to run JDK17
    }
}

version = '1.1.3'

repositories {
    //flatDir {
    //    dirs '../cst-bindings/build/libs'
    //}
    mavenCentral()
    maven {
        url = "https://github.com/rosjava/rosjava_mvn_repo/raw/master"
    }
    maven {
        url = "https://artifacts.camunda.com/artifactory/public/"
    }
    maven { url = 'https://jitpack.io' }
    maven {
        url = 'https://cst-group.github.io/cst-dependencies/maven-repo/'
    }
}

configurations {
    extraLibs
}

dependencies {
    //api('com.github.CST-Group:cst:1.6.0')
    api('com.github.CST-Group:cst-bindings:1.1.8')
    //api(':cst-bindings:1.1.1-full')
    api group: 'org.jfree', name: 'jfreechart', version: '1.0.19'
    api 'com.soartech:jsoar-core:4.1.3'
    api 'com.soartech:jsoar-debugger:4.1.3'
    api group: 'net.sf.jung', name: 'jung-algorithms', version: '2.0.1'
    api group: 'net.sf.jung', name: 'jung-graph-impl', version: '2.0.1'
    api group: 'net.sf.jung', name: 'jung-visualization', version: '2.0.1'
    api group: 'net.sf.jung', name: 'jung-api', version: '2.0.1'
    testImplementation 'com.github.CST-Group:cst:1.6.0'
    testImplementation 'com.1stleg:jnativehook:2.1.0'
    // These are necessary for JUnit5 tests
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.11.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
    from {
    	configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    javadoc.options.addStringOption('Xdoclint:none', '-quiet') // this is to avoid complaints about documentation missing parameter description
}
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
task uberJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier = 'full'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}
artifacts
        {
            archives javadocJar, sourcesJar, uberJar
        }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

uberJar {
    dependsOn jar
}

test {
    useJUnitPlatform()
}
