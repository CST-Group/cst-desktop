plugins {
    id 'java-library-distribution'
    id 'jacoco'
    id 'maven-publish'
}

group = 'com.github.CST-Group'

description = "CST-Desktop"

sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.0.6'

repositories {
    flatDir {
        dirs '../cst-bindings/build/libs'
    }
    mavenCentral()
    maven {
        url "https://github.com/rosjava/rosjava_mvn_repo/raw/master"
    }
    maven {
        url "https://artifacts.camunda.com/artifactory/public/"
    }
    maven { url 'https://jitpack.io' }
}

configurations {
    extraLibs
}

dependencies {
    api('com.github.CST-Group:cst:2032096')
    api('com.github.CST-Group:cst-bindings:6a68f3d')
    //api ':cst-bindings:1.0.6-full'
    api group: 'org.jfree', name: 'jfreechart', version: '1.0.19'
    api 'com.soartech:jsoar-core:4.1.3'
    api 'com.soartech:jsoar-debugger:4.1.3'
    //api 'org.ros.rosjava_core:rosjava:0.3.6'
    //implementation 'com.github.rosjava:rosjava_core:0.3.7'
    api group: 'net.sf.jung', name: 'jung-algorithms', version: '2.0.1'
    api group: 'net.sf.jung', name: 'jung-graph-impl', version: '2.0.1'
    api group: 'net.sf.jung', name: 'jung-visualization', version: '2.0.1'
    api group: 'net.sf.jung', name: 'jung-api', version: '2.0.1'
    api 'com.github.rosjava:rosjava_core:0.3.7'
    api 'org.ros.rosjava_bootstrap:message_generation:0.3.3'
    testImplementation 'com.github.CST-Group:cst:1.4.2'
    testImplementation 'com.1stleg:jnativehook:2.1.0'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.ros.rosjava_messages:std_msgs:0.5.11'
    testImplementation 'org.ros.rosjava_messages:rosjava_test_msgs:0.3.0'
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
    from {
    	configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    javadoc.options.addStringOption('Xdoclint:none', '-quiet') // this is to avoid complaints about documentation missing parameter description
}
task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}
task uberJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier = 'full'
    from sourceSets.main.output
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}
artifacts
        {
            archives javadocJar, sourcesJar, uberJar
        }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}
